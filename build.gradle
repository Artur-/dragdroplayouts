apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'eclipse-wtp'

// Global settings
sourceCompatibility = 1.5
targetCompatibility = 1.5
group = 'fi.jasoft.dragdroplayouts'
version = 'development'

// Demo settings
demoWidgetsetClass = 'fi.jasoft.dragdroplayouts.demo.DemoWidgetSet'
webAppDirName = 'WebContent'

// Addon settings
widgetsetClass = 'fi.jasoft.dragdroplayouts.DragDropLayoutsWidgetSet'
addonName = 'DragDropLayouts'
addonAuthor = 'John Ahlroos'
addonLicence = 'Apache 2.0'
archivesBaseName = 'dragdroplayouts'

// Sources
sourceSets{
    main{
        java{
            srcDir 'src'
            srcDir 'demo'
        }
    }
}

// Repositories
repositories{
    mavenCentral()
    mavenRepo url: 'http://maven.vaadin.com/vaadin-addons'
}

// Dependencies
dependencies{
    compile group:'com.vaadin',         name:'vaadin',          version:'6.7.5'
    compile group:'com.google.gwt',     name:'gwt-user',        version:'2.3.0'
    compile group:'com.google.gwt',     name:'gwt-dev',         version:'2.3.0'
    compile group:'javax.validation',   name:'validation-api',  version:'1.0.0.GA'
    compile group:'javax.validation',   name:'validation-api',  version:'1.0.0.GA',     classifier:'sources'
    compile group:'org.vaadin.addons',  name:'codelabel',       version:'1.0'
}


/*
 *  Widgetset compilation needed by the war task
 */
task widgetset << {
        // Target directory for the build result
        targetDir = new File('WebContent/VAADIN/widgetsets')
         
        // Compile
        ant.java(classname:'com.google.gwt.dev.Compiler',failOnError: 'yes', fork: 'true', maxmemory: '512m')
        {
                classpath {
                    pathElement(path: configurations.compile.asPath)
                    pathElement(path: sourceSets.main.runtimeClasspath.asPath)
                    sourceSets.main.java.srcDirs.each{
                        pathelement(location:it.absolutePath)
                    }
                }

                jvmarg(value: '-Xmx1024M')
                jvmarg(value: '-Xms512M')

                arg(line: '-draftCompile')
                arg(line: '-logLevel INFO')
                arg(line: '-style PRETTY')
                arg(line: '-localWorkers 2')
                arg(line: '-war '+targetDir)
                arg(line: demoWidgetsetClass)
        }
}

/*
 * Builds the deployable demo application
 */
war{
        
    // Include widgetset
    dependsOn widgetset
    
    // Include sources
    from sourceSets.main.allJava 
    
    // WAR filename
    archiveName = addonName + '.war'
}

/*
 * Builds the Vaadin addon package
 */
jar{
    
    // Include sources
    from sourceSets.main.allJava
    
    // Exclude demo sources
    exclude 'fi/jasoft/dragdroplayouts/**/demo/**/*'
    
    // Create manifest
    manifest{
        attributes(
            'Vaadin-Package-Version': 1,
            'Vaadin-Widgetsets': widgetsetClass,
            'Vaadin-License-Title': addonLicence,
            'Implementation-Title': addonName,
            'Implementation-Version': version,
            'Implementation-Vendor': addonAuthor,
        )
    }
}