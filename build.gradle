allprojects {
  group='fi.jasoft'
  version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'development';
  apply plugin: 'eclipse-wtp'
}

subprojects {
  apply from: 'http://plugins.jasoft.fi/vaadin.plugin'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
  
  //FIXME remove when vaadin plugin supports release repository
  repositories {
	maven {
		url 'https://maven.vaadin.com/vaadin-prereleases'
	}
  }

  vaadin {
    version = project.hasProperty('VAADIN_VERSION') ? getProperty('VAADIN_VERSION') : '7.6.0.beta1'
  }
}

task wrapper(type: Wrapper) {
        gradleVersion = '2.8'
}

/**
 * Vaadin addon project
 */
project(':addon'){
  apply plugin: 'maven'
  apply plugin: 'osgi'

  configurations {
      deploy
  }

  dependencies{
    testCompile     group:'junit',  name:'junit',   version: '4.8.+'
    deploy 'org.apache.maven.wagon:wagon-ssh:2.2'
  }

  vaadin {
    widgetset = 'fi.jasoft.dragdroplayouts.DragDropLayoutsWidgetSet'

    addon {
      author 'John Ahlroos'
      license 'Apache 2.0'
      title 'DragDropLayouts'
    }
  }

  jar {
    baseName = 'dragdroplayouts'
    dependsOn test

    // Include sources
    sourceSets.main.java.srcDirs.each{
      from it
    }

    manifest {
      // the manifest of the default jar is of type OsgiManifest
      name = 'dragdroplayouts'
      /*
       it seems we need to export the client-side stuff too to make it easier to include the jar file as project dependency
       instruction 'Export-Package', '!fi.jasoft.dragdroplayouts.client.*', '*'
       */
      instruction 'Import-Package', '!com.google.gwt.*', '!com.vaadin.client.*', '*'
      instruction 'Bundle-Vendor', 'johndevs'
      instruction 'Bundle-Description', 'Drag and drop layouts addon'
      instruction 'Bundle-DocURL', 'https://github.com/johndevs/dragdroplayouts'
    }
  }

  task createAddonPom {
    pom{ artifactId = 'dragdroplayouts' }.writeTo("META-INF/maven/fi.jasoft/dragdroplayouts/pom.xml")
  }

  artifacts {
      archives jar
  }

  uploadArchives {
      dependsOn createAddonPom
      repositories.mavenDeployer {
          def snapshotRepositoryUrl = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY')
          def snapshotRepositoryUser = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_USER')
          def snapshotRepositoryPassword = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_PASSWORD')
          def snapshotRepositoryPrivateKey = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_KEY')

          configuration = configurations.deploy

          snapshotRepository( url: snapshotRepositoryUrl ) {
             authentication(
                     userName: snapshotRepositoryUser,
                     password: snapshotRepositoryPassword,
                     privateKey: snapshotRepositoryPrivateKey
             )
          }

          addFilter('jar') {artifact, file ->
              artifact.ext == 'jar'
          }
      }
  }
}


/*
 * Demo application for demonstrating the addon
 */
project(':demo'){
  apply plugin: 'war'
  apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'
    
  dependencies {
    compile project(':addon')
    compile group:'de.sven-jacobs', name:'loremipsum', version:'1.0'
    compile group:'de.java2html',   name:'java2html',  version:'5.0'
  }

  vaadin {
    widgetset = 'fi.jasoft.dragdroplayouts.demo.DemoWidgetSet'
    serverPort 7676
    debug true

    devmode {
      superDevMode true
    }

    plugin {
      logToConsole true
    }
  }

  war {
    // WAR filename
    archiveName = 'DragDropLayouts7.war'

    // Include widgetset
    dependsOn test, vaadinCompileWidgetset

    // Add sources
    webInf{
      into('classes'){
        from sourceSets.main.allJava
       }
    }
  }

  test {
    dependsOn vaadinCompileWidgetset
    systemProperties = System.properties
  }
}
